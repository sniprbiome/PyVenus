//----------------------------------------------------------------------------------------
// Copyright (C) by HAMILTON Bonaduz AG, CH-7402 Bonaduz.
// All rights reserved.
//
// Project              : HSL Extension Framework
// Description          : Framework library with 2-dimensional array functions
//
// Software Version     : 4.3
//
// Revision history:
// - Oliver Funke   2011.02.12   library created
//
//----------------------------------------------------------------------------------------

#ifndef __HSLEXTENSIONS_ARRAY2D__
#define __HSLEXTENSIONS_ARRAY2D__ 1

//============================== includes ==============================
#ifndef __HSLEXTENSIONS_FRAMEWORK__
   #include "HSLExtensions/Framework/HSLExtensionsFramework.hsl"
#endif

#ifndef __HSLEXTENSIONS_FRAMEWORK_ENUMS__
   #include "HSLExtensions/Framework/Enumerators.hsl"
#endif

#ifndef __HSLStrLib_hsl__
   #include "HSLStrLib.hsl"
#endif


//============================== HSLExtension ==============================
namespace HSLExtensions
{
   //============================== Array2D ==============================
   namespace Array2D
   {
      //------------------------------------------------------
      // Get the size of the array
      //
      // throws an exception if
      // - input is not a 2d array      
      //
      // [in] i_arrValues: array
      // [out] o_intSizeX: size of x dimension (1..x)
      // [out] o_intSizeY: size of y dimension (1..x)
      //------------------------------------------------------
      private function _GetSize(variable i_arrValues[], variable& o_intSizeX, variable& o_intSizeY) void
      {
         variable intSizeTotal;
         variable varSizeX;
         variable varSizeY;

         //init
         o_intSizeX = 0;
         o_intSizeY = 0;
         
         //get size total
         intSizeTotal = i_arrValues.GetSize();

         if(intSizeTotal < 2)
         {
            err.Raise(3,"The parameter 'i_arrValues[]' is not a 2D Array");
         }

         //get size x and y
         varSizeX = i_arrValues[0];
         varSizeY = i_arrValues[1];
         
         if(HSLExtensions::Framework::Types::IsInteger(varSizeX) == hslFalse)
         {
            err.Raise(3,"The parameter 'i_arrValues[]' is not a 2D Array");
         }

         if(HSLExtensions::Framework::Types::IsInteger(varSizeY) == hslFalse)
         {
            err.Raise(3,"The parameter 'i_arrValues[]' is not a 2D Array");
         }

         if((varSizeX*varSizeY + 2) != intSizeTotal)
         {
            err.Raise(3,"The parameter 'i_arrValues[]' is not a 2D Array");
         }

         //set return values
         o_intSizeX = varSizeX;
         o_intSizeY = varSizeY;
      }

      //------------------------------------------------------
      // Creates a 2 dimensional array
      //
      // throws an exception if
      // - input has wrong type
      // - input is out of range
      //
      // [in] i_intSizeX: size of x dimension (1..x)
      // [in] i_intSizeY: size of y dimension (1..x)
      // [return] array
      //------------------------------------------------------
      function Create(variable i_intSizeX, variable i_intSizeY) variable[]
      {
         variable strFunctionName(GetFunctionName());
         variable arrValues[];

         //init
         arrValues.SetSize(0);
         
         //trace input
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_intSizeX", i_intSizeX);
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_intSizeY", i_intSizeY);
         
         //check input
         if(HSLExtensions::Framework::Types::IsInteger(i_intSizeX) == hslFalse)
         {
            err.Raise(1,"The parameter 'i_intSizeX' has a wrong type");
         }

         if(HSLExtensions::Framework::Types::IsInteger(i_intSizeY) == hslFalse)
         {
            err.Raise(1,"The parameter 'i_intSizeY' has a wrong type");
         }

         if(i_intSizeX < 1)
         {
            err.Raise(2,"The parameter 'i_intSizeX' is out of range");
         }

         if(i_intSizeY < 0)
         {
            err.Raise(2,"The parameter 'i_intSizeY' is out of range");
         }

         //create aray - add 2 header values and store dimensions
         arrValues.SetSize(i_intSizeX * i_intSizeY + 2);
         arrValues[0] = i_intSizeX;
         arrValues[1] = i_intSizeY;

         //trace output
         HSLExtensions::Framework::Trace::FunctionResultArrayTrace(strFunctionName, arrValues);

         return(arrValues);
      }

      //------------------------------------------------------
      // Get the size of the array
      //
      // throws an exception if
      // - input is not a 2d array      
      //
      // [in] i_arrValues: array
      // [out] o_intSizeX: size of x dimension (1..x)
      // [out] o_intSizeY: size of y dimension (1..x)
      //------------------------------------------------------
      function GetSize(variable i_arrValues[], variable& o_intSizeX, variable& o_intSizeY) void
      {
         variable strFunctionName(GetFunctionName());
         
         //trace input
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "i_arrValues", i_arrValues);
         
         //get size 
         _GetSize(i_arrValues, o_intSizeX, o_intSizeY);
         
         //trace output
         HSLExtensions::Framework::Trace::FunctionOutputParameterTrace(strFunctionName, "o_intSizeX", o_intSizeX);
         HSLExtensions::Framework::Trace::FunctionOutputParameterTrace(strFunctionName, "o_intSizeY", o_intSizeY);
      }

      //------------------------------------------------------
      // Initialies all values of the array by setting the given value
      //
      // throws an exception if
      // - input is not a 2d array      
      //
      // [in] i_arrValues: array to initialze
      // [in] i_varValue: value which shall be set (all types are allowed)
      //------------------------------------------------------
      function InitializeAllValues(variable& io_arrValues[], variable i_varValue) void
      {
         variable strFunctionName(GetFunctionName());
         variable intSizeTotal;
         variable intSizeX;
         variable intSizeY;
         variable i;

         //trace input
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "i_arrValues", io_arrValues);
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_varValue", i_varValue);
         
         //get size 
         _GetSize(io_arrValues, intSizeX, intSizeY);

         //set values
         intSizeTotal = intSizeX * intSizeY + 2;

         for(i=2; i<intSizeTotal; i++)
         {
            io_arrValues.SetAt(i, i_varValue);
         }

         //trace output
         HSLExtensions::Framework::Trace::FunctionResultArrayTrace(strFunctionName, io_arrValues);
      }

      //------------------------------------------------------
      // Set the array value
      //
      // throws an exception if
      // - input is not a 2d array      
      // - input has wrong type
      // - input is out of range
      //
      // [in/out] io_arrValues: array
      // [in] i_intPositionX: position in x dimension (1..x)
      // [in] i_intPositionY: position in y dimension (1..x)
      // [in] i_varValue: value to set
      //------------------------------------------------------
      function SetValue(variable& io_arrValues[], 
                        variable i_intPositionX, 
                        variable i_intPositionY, 
                        variable i_varValue) void
      {
         variable strFunctionName(GetFunctionName());

         variable intSizeTotal;
         variable intSizeX;
         variable intSizeY;

         variable intPosition;
        
         //trace input
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "io_arrValues", io_arrValues);
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_intPositionX", i_intPositionX);
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_intPositionY", i_intPositionY);
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_varValue", i_varValue);
         
         //check input
         if(HSLExtensions::Framework::Types::IsInteger(i_intPositionX) == hslFalse)
         {
            err.Raise(1,"The parameter 'i_intPositionX' has a wrong type");
         }

         if(HSLExtensions::Framework::Types::IsInteger(i_intPositionY) == hslFalse)
         {
            err.Raise(1,"The parameter 'i_intPositionY' has a wrong type");
         }

         //get size 
         _GetSize(io_arrValues, intSizeX, intSizeY);

         //check range
         if( (i_intPositionX < 1) ||
             (i_intPositionX > intSizeX) )
         {
            err.Raise(2,"The parameter 'i_intPositionX' is out of range");
         }

         if( (i_intPositionY < 1) ||
             (i_intPositionY > intSizeY) )
         {
            err.Raise(2,"The parameter 'i_intPositionY' is out of range");
         }

         //calculate position
         intPosition = (i_intPositionY - 1) * intSizeX + i_intPositionX + 1;

         //set value
         io_arrValues[intPosition] = i_varValue;

         //trace output
         HSLExtensions::Framework::Trace::FunctionOutputArrayTrace(strFunctionName, "io_arrValues", io_arrValues);
      }

      //------------------------------------------------------
      // Get the array value
      //
      // throws an exception if
      // - input is not a 2d array      
      // - input has wrong type
      // - input is out of range
      //
      // [in] i_arrValues: array
      // [in] i_intPositionX: position in x dimension (1..x)
      // [in] i_intPositionY: position in y dimension (1..x)
      // [return] value 
      //------------------------------------------------------
      function GetValue(variable i_arrValues[], 
                        variable i_intPositionX, 
                        variable i_intPositionY) variable
      {
         variable strFunctionName(GetFunctionName());

         variable intSizeTotal;
         variable intSizeX;
         variable intSizeY;

         variable intPosition;

         variable varValue;

         //trace input
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "i_arrValues", i_arrValues);
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_intPositionX", i_intPositionX);
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_intPositionY", i_intPositionY);
         
         //check input
         if(HSLExtensions::Framework::Types::IsInteger(i_intPositionX) == hslFalse)
         {
            err.Raise(1,"The parameter 'i_intPositionX' has a wrong type");
         }

         if(HSLExtensions::Framework::Types::IsInteger(i_intPositionY) == hslFalse)
         {
            err.Raise(1,"The parameter 'i_intPositionY' has a wrong type");
         }

         //get size 
         _GetSize(i_arrValues, intSizeX, intSizeY);

         //check range
         if( (i_intPositionX < 1) ||
             (i_intPositionX > intSizeX) )
         {
            err.Raise(2,"The parameter 'i_intPositionX' is out of range");
         }

         if( (i_intPositionY < 1) ||
             (i_intPositionY > intSizeY) )
         {
            err.Raise(2,"The parameter 'i_intPositionY' is out of range");
         }

         //calculate position
         intPosition = (i_intPositionY - 1) * intSizeX + i_intPositionX + 1;

         //get value
         varValue = i_arrValues[intPosition];

         //trace result
         HSLExtensions::Framework::Trace::FunctionResultParameterTrace(strFunctionName, varValue);

         return(varValue);
      }

      //------------------------------------------------------
      // Get all values of a row (all values of the x dimension)
      //
      // throws an exception if
      // - input is not a 2d array      
      // - input has wrong type
      // - input is out of range
      //
      // [in] i_arrValues: array
      // [in] i_intPositionY: position in y dimension (1..x)
      // [return] value 
      //------------------------------------------------------
      function GetRow(variable i_arrValues[], 
                        variable i_intPositionY) variable[]
      {
         variable strFunctionName(GetFunctionName());
         variable arrValues[];

         variable intSizeTotal;
         variable intSizeX;
         variable intSizeY;

         variable intPosition;

         variable i;

         //trace input
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "i_arrValues", i_arrValues);
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_intPositionY", i_intPositionY);
         
         //check input
         if(HSLExtensions::Framework::Types::IsInteger(i_intPositionY) == hslFalse)
         {
            err.Raise(1,"The parameter 'i_intPositionY' has a wrong type");
         }

         //get size 
         _GetSize(i_arrValues, intSizeX, intSizeY);

         //check range
         if( (i_intPositionY < 1) ||
             (i_intPositionY > intSizeY) )
         {
            err.Raise(2,"The parameter 'i_intPositionY' is out of range");
         }

         //create array
         for(i = 0; i<intSizeX; i++)
         {
            intPosition = (i_intPositionY - 1) * intSizeX + i + 2;

            arrValues.AddAsLast(i_arrValues.GetAt(intPosition));
         }
         
         //trace result
         HSLExtensions::Framework::Trace::FunctionResultArrayTrace(strFunctionName, arrValues);

         return(arrValues);
      }

      //------------------------------------------------------
      // Get all values of a column (all values of the y dimension)
      //
      // throws an exception if
      // - input is not a 2d array      
      // - input has wrong type
      // - input is out of range
      //
      // [in] i_arrValues: array
      // [in] i_intPositionX: position in x dimension (1..x)
      // [return] value 
      //------------------------------------------------------
      function GetColumn(variable i_arrValues[], 
                        variable i_intPositionX) variable[]
      {
         variable strFunctionName(GetFunctionName());
         variable arrValues[];

         variable intSizeTotal;
         variable intSizeX;
         variable intSizeY;

         variable intPosition;

         variable i;

         //trace input
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "i_arrValues", i_arrValues);
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_intPositionX", i_intPositionX);
         
         //check input
         if(HSLExtensions::Framework::Types::IsInteger(i_intPositionX) == hslFalse)
         {
            err.Raise(1,"The parameter 'i_intPositionX' has a wrong type");
         }

         //get size 
         _GetSize(i_arrValues, intSizeX, intSizeY);

         //check range
         if( (i_intPositionX < 1) ||
             (i_intPositionX > intSizeX) )
         {
            err.Raise(2,"The parameter 'i_intPositionX' is out of range");
         }

         //create array
         for(i = 0; i<intSizeY; i++)
         {
            intPosition = i * intSizeX + i_intPositionX + 1;

            arrValues.AddAsLast(i_arrValues.GetAt(intPosition));
         }
         
         //trace result
         HSLExtensions::Framework::Trace::FunctionResultArrayTrace(strFunctionName, arrValues);

         return(arrValues);
      }

      //------------------------------------------------------
      // Set all values of a row (all values of the x dimension)
      //
      // throws an exception if
      // - input is not a 2d array      
      // - input has wrong type
      // - input is out of range
      //
      // [in] io_arrValues: array
      // [in] i_arrRow: array
      // [in] i_intPositionY: position in y dimension (1..x)
      // [return] no return value 
      //------------------------------------------------------
      function SetRow(variable io_arrValues[],
                        variable i_arrRow[],
                        variable i_intPositionY) void
      {
         variable strFunctionName(GetFunctionName());

         variable intSizeTotal;
         variable intSizeX;
         variable intSizeY;

         variable intSizeRow;

         variable intPosition;

         variable i;

         //trace input
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "io_arrValues", io_arrValues);
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "i_arrRow", i_arrRow);
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_intPositionY", i_intPositionY);
         
         //check input
         if(HSLExtensions::Framework::Types::IsInteger(i_intPositionY) == hslFalse)
         {
            err.Raise(1,"The parameter 'i_intPositionY' has a wrong type");
         }

         //get size 
         _GetSize(io_arrValues, intSizeX, intSizeY);

         intSizeRow = i_arrRow.GetSize();

         //check range
         if( (i_intPositionY < 1) ||
             (i_intPositionY > intSizeY) )
         {
            err.Raise(2,"The parameter 'i_intPositionY' is out of range");
         }

         //check size of input
         if(intSizeRow != intSizeX)
         {
            err.Raise(2,"The number of parameters int the input array 'i_arrRow' does not match with the 2DArray size");
         }


         //set values array
         for(i = 0; i<intSizeX; i++)
         {
            intPosition = (i_intPositionY - 1) * intSizeX + i + 2;

            io_arrValues.SetAt(intPosition, i_arrRow.GetAt(i));
         }
         
         //trace result
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "io_arrValues after update", io_arrValues);
      }

      //------------------------------------------------------
      // Set all values of a column (all values of the y dimension)
      //
      // throws an exception if
      // - input is not a 2d array      
      // - input has wrong type
      // - input is out of range
      //
      // [in] io_arrValues: array
      // [in] i_arrColumn: array
      // [in] i_intPositionX: position in x dimension (1..x)
      // [return] no return value 
      //------------------------------------------------------
      function SetColumn(variable io_arrValues[],
                        variable i_arrColumn[],
                        variable i_intPositionX) void
      {
         variable strFunctionName(GetFunctionName());

         variable intSizeTotal;
         variable intSizeX;
         variable intSizeY;

         variable intSizeColumn;

         variable intPosition;

         variable i;

         //trace input
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "io_arrValues", io_arrValues);
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "i_arrColumn", i_arrColumn);
         HSLExtensions::Framework::Trace::FunctionInputParameterTrace(strFunctionName, "i_intPositionX", i_intPositionX);
         
         //check input
         if(HSLExtensions::Framework::Types::IsInteger(i_intPositionX) == hslFalse)
         {
            err.Raise(1,"The parameter 'i_intPositionX' has a wrong type");
         }

         //get size 
         _GetSize(io_arrValues, intSizeX, intSizeY);

         intSizeColumn = i_arrColumn.GetSize();

         //check range
         if( (i_intPositionX < 1) ||
             (i_intPositionX > intSizeX) )
         {
            err.Raise(2,"The parameter 'i_intPositionX' is out of range");
         }

         //check size of input
         if(intSizeColumn != intSizeY)
         {
            err.Raise(2,"The number of parameters int the input array 'i_arrColumn' does not match with the 2DArray size");
         }

         //set values of array
         for(i = 0; i<intSizeY; i++)
         {
            intPosition = i * intSizeX + i_intPositionX + 1;

            io_arrValues.SetAt(intPosition, i_arrColumn.GetAt(i));
         }
         
         //trace result
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "io_arrValues after update", io_arrValues);
      }

      //------------------------------------------------------
      // Add a row to the table (increases the y dimension and adds all values of the x dimension)
      //
      // throws an exception if
      // - input is not a 2d array      
      // - input has wrong type
      // - input is out of range
      //
      // [in] io_arrValues: array
      // [in] i_arrRow: array
      // [return] no return value 
      //------------------------------------------------------
      function AddRow(variable io_arrValues[],
                        variable i_arrRow[]) void
      {
         variable strFunctionName(GetFunctionName());

         variable intSizeTotal;
         variable intSizeX;
         variable intSizeY;

         variable intSizeRow;

         variable intPosition;

         variable i;

         //trace input
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "io_arrValues", io_arrValues);
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "i_arrRow", i_arrRow);         
         
         //get size 
         _GetSize(io_arrValues, intSizeX, intSizeY);

         intSizeRow = i_arrRow.GetSize();

         //check size of input
         if(intSizeRow != intSizeX)
         {
            err.Raise(2,"The number of parameters int the input array 'i_arrRow' does not match with the 2DArray size");
         }

         //add values to array
         for(i = 0; i<intSizeRow; i++)
         {            
            io_arrValues.AddAsLast(i_arrRow.GetAt(i));
         }

         //increase array size
         io_arrValues[1] = io_arrValues.GetAt(1) + 1;

         //trace result
         HSLExtensions::Framework::Trace::FunctionInputArrayTrace(strFunctionName, "io_arrValues after update", io_arrValues);
      }

   } //end Array2D
} //end HSLExtension

#endif //end __HSLEXTENSION_ARRAY2D__

// $$author=ofunke$$valid=1$$time=2013-01-31 10:48$$checksum=da761812$$length=083$$